## 列偏移位置和性能

### 构建测试数据
set serveroutput on;
set echo on
drop table t purge;
declare
  l_sql varchar2(1000);
begin
  l_sql := 'create table t(';
  for i in 1..25
  loop
    l_sql := l_sql || 'n' || i || ' number,';
  end loop;
  l_sql := l_sql || 'pad varchar2(1000)) pctfree 10';
  execute immediate l_sql;
end;
/

declare
  l_sql varchar2(1000);
begin
  l_sql := 'insert into t select ';
  for i in 1..25
  loop
    l_sql := l_sql || '0,';
  end loop;
  l_sql := l_sql ||' null from dual connect by level <= 10000';
  execute immediate l_sql;
  commit;
end;
/

execute dbms_stats.gather_table_stats(ownname=>user,tabname=>'t')
select num_rows,blocks from user_tables where table_name ='T';

declare
  l_dumy pls_integer;
  l_start pls_integer;
  l_stop pls_integer;
  l_sql varchar2(100);
begin
  l_start := dbms_utility.get_time;
  for j in 1..1000
  loop
    execute immediate 'select count(*) from t' into l_dumy;
  end loop;
  l_stop := dbms_utility.get_time;
  dbms_output.put_line((l_stop - l_start)/100);
  
  for i in 1..25
  loop
    l_sql := 'select count(n' || i || ') from t';
    l_start := dbms_utility.get_time;
    for j in 1..1000
    loop
      execute immediate l_sql into l_dumy;
    end loop;
    l_stop := dbms_utility.get_time;
    dbms_output.put_line((l_stop - l_start)/100);
  end loop;
end;
/

### 执行结果
.62
.58
.64
.73
.71
.61
.74
.79
.72
.76
.8
.87
.87
.82
.8
.82
.83
.84
.85
.98
.92
.88
.99
1.81
2.08
1.48

### 结论  
count(*)的性能是比较高的。查询靠前的列所需要的时间少，所以经常访问的列最好设计在靠前的列偏移位置。
